import QtQuick 2.15
import QtQuick.Layouts
import Quantum

Rectangle {
    id: root

    property string fileName
    property string downloadID
    property int downloadProgress
    property string speed
    property string downloaded
    property string fileSize
    property string status
    property string rta
    property bool isCompleted: false
    property bool isPaused: false
    property int delayTargetButton: 0           // 0 As default, 1 for the pause/resume button, 2 for the cancel button.

    signal buttonClicked()
    signal cancelClicked()

    height: 100
    width: ListView.view ? ListView.view.width : parent.width

    color: Appearance.surface
    border.color: Appearance.borderAccent
    border.width: 2
    radius: 5

    FontLoader {
        id: appFont

        source: "qrc:/qml/assets/fonts/Lexend.ttf"
    }

    Timer {
        id: delayTimer
        interval: 800
        repeat: false

        onTriggered: {
            if (delayTargetButton == 1)
                pauseButton.buttonEnabled = true
            else if (delayTargetButton == 2)
                cancelButton.buttonEnabled = true
            delayTargetButton = 0
        }
    }

    RowLayout {
        anchors.fill: parent

        anchors.margins: 15

        spacing: 15

        Text {
            id: fileNameText

            text: fileName

            Layout.fillWidth: true
            Layout.minimumWidth: 0
            elide: Text.ElideRight

            color: "white"
            font.family: appFont.name
            font.pixelSize: 20
        }

        ColumnLayout {
            Layout.preferredWidth: progressBar.width

            RowLayout {
                Layout.preferredWidth: progressBar.width

                Text {
                    id: remainingTime

                    Layout.alignment: Qt.AlignLeft

                    text: downloadProgress === 100 ? "Completed" : rta

                    color: "gray"
                    font.family: appFont.name
                    font.pointSize: 10
                }

                Item {
                    Layout.preferredWidth: progressBar.width - fileSizeText.width - fileNameText.width
                }

                Text {
                    id: speedText

                    Layout.alignment: Qt.AlignRight

                    text: downloadProgress === 100 ? "" : speed

                    color: "gray"
                    font.family: appFont.name
                    font.pixelSize: 10
                }
            }

            ProgressBar {
                id: progressBar

                progress: downloadProgress
            }

            RowLayout {
                Layout.preferredWidth: progressBar.width

                Text {
                    id: fileSizeText

                    Layout.alignment: Qt.AlignRight

                    text: fileSize

                    color: "gray"
                    font.family: appFont.name
                    font.pixelSize: 10
                }
            }
        }

        ColumnLayout {
            Layout.fillHeight: true
            Layout.preferredWidth: pauseButton.width

            UiButton {
                id: pauseButton

                buttonHeight: 32
                buttonWidth: 100

                buttonText: isCompleted ? "Open" : isPaused ? "Resume" : "Pause"
                buttonIcon: isCompleted ? "qrc:/qml/assets/icons/play.svg" : isPaused ? "qrc:/qml/assets/icons/play.svg" : "qrc:/qml/assets/icons/pause.svg"

                onClicked: {
                    buttonEnabled = false
                    root.buttonClicked()
                    root.delayTargetButton = 1
                    delayTimer.start()
                }
            }

            UiButton {
                id: cancelButton

                buttonHeight: 32
                buttonWidth: 100

                buttonText: isCompleted ? "Remove" : "Cancel"
                buttonIcon: "qrc:/qml/assets/icons/close.svg"

                onClicked: {
                    buttonEnabled = false
                    root.cancelClicked()
                    root.delayTargetButton = 2
                    delayTimer.start()
                }
            }
        }
    }
}
